// Nocke (Nok-uh) HVB-X Chainrail
// Nocke Armaments' pride and joy, the Chainrail brings the instant-hit capability of a railgun to the average soldier,
// at a cyclic rate of just under 60 RPM. The HVB-X's 14mm tungsten darts are guaranteed to put a dent in anyone's day,
// if not all the way through it. The belt-feed means that the average soldier rarely has to reload under normal combat
// conditions, and the caseless and linkless ammo means little or no waste left on the battlefield.
//
// Nocke Armaments - Nock and Loose!
// Ask about our high-risk contractor discounts!
//
//
Class Chainrail : DoomWeapon {	
	default {
		inventory.pickupMessage "Coralled a ChainRail!";
		weapon.slotNumber 4;
		weapon.SelectionOrder 4000;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 40;
		Weapon.Kickback 500;
		Weapon.AmmoType "Cell";
		+Weapon.DontBob;
		+WEAPON.NODEATHINPUT;
		Obituary "%o got railed by %k's Chainrail.";
	}
	states {
		Ready:
			CHRL A 1 A_WeaponReady();
			Loop;
		Select:
			CHRL A 1 A_Raise();
			Loop;
		Deselect:
			CHRL A 1 A_Lower();
			Loop;
		Fire:
			CHRL A 1 Offset(0,34);
			TNT1 A 0 {
				A_RailAttack(77, 0, false, "darkorchid", "aquamarine1", RGF_SILENT | RGF_NORANDOMPUFFZ, 0, "CHRLPuff", frandom(-0.2,0.2), frandom(-0.2,0.2), 0, 15, 1.2, 2.5, "none", 5, 180, 3);
				A_PlaySound("weapons/chrl_fire1", CHAN_WEAPON);
				A_TakeInventory("Cell", 1);
			}
			// recoil
			CHRL A 3 A_WeaponOffset(0, 12, WOF_ADD);
			CHRL A 3 A_WeaponOffset(0, 8, WOF_ADD);
			CHRL A 4 A_WeaponOffset(0, 6, WOF_ADD);
			CHRL A 6 A_WeaponOffset(0, 32);
			CHRL A 0 A_PlaySound("weapons/chrl_rechamber", CHAN_BODY);
			CHRL B 5 A_WeaponOffset(-4, 6, WOF_ADD);
			CHRL CD 5;
			CHRL A 2 A_WeaponOffset(5, 4, WOF_ADD);
			CHRL A 2 A_WeaponOffset(-3, 2, WOF_ADD);
			CHRL A 1 A_WeaponOffset(0, 32);
			CHRL A 1 A_WeaponReady();
			Goto Ready;
		Spawn:
			TLGL A -1; //no pickup sprite yet
			Stop;
	}
}

class CHRLPuff : BulletPuff {
	default { 
		+ALWAYSPUFF
	}
}